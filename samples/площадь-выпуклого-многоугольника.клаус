программа ПлощадьВыпуклогоМногоугольника;

    функция площадьТреугольника(xa, ya, xb, yb, xc, yc: дробное): дробное;
    переменная
        a, b, c, p: дробное;
    начало
        a:= ((xb-xa)^2 + (yb-ya)^2)^0.5;
        b:= ((xc-xb)^2 + (yc-yb)^2)^0.5;
        c:= ((xc-xa)^2 + (yc-ya)^2)^0.5;
        
        p:= (a + b + c)/2;
        вернуть (p*(p-a)*(p-b)*(p-c))^0.5;
    окончание;

тип
    Точка/Точек = структура
        x: дробное;
        y: дробное;
    окончание;
переменные
    кво, к: целое;
    xa, ya, xb, yb, xc, yc: дробные;
    тчк: массив Точек;
    плщ: дробное;
начало
    сообщить("Программа вычисляет площадь выпуклого многоугольника.", НС);
    сообщить("Введите кол-во углов: ");
    ввести(кво);
    длина(тчк, кво);
    сообщить("Введите координаты углов по порядку друг за другом:", НС);
    для к от 0 до кво-1 цикл начало
        сообщить(формат("Введите Х%ц, У%ц", к+1, к+1));
        ввести(тчк[к].x, тчк[к].y);
    конец;
    плщ := 0;
    для к от 2 до кво-1 цикл начало
        xa := тчк[0].x;
        ya := тчк[0].y;
        xb := тчк[к].x;
        yb := тчк[к].y;
        xc := тчк[к-1].x;
        yc := тчк[к-1].y;
        плщ := плщ + площадьТреугольника(xa, ya, xb, yb, xc, yc);
    конец;
    сообщить("Площадь многоугольника: ");
    вывести(плщ);
    ничего;
окончание.
