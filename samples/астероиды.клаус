программа Астероиды;

тип
    // Точка с дробными координатами
    ДрТочка = структура 
        г, в: дробные; 
    окончание;
    
    // Глобальные параметры игры
    Параметры = структура
        экран: Размер;           // размеры экрана (в точках)
        скорФона: целое;         // скорость движения фона
        задержка: целое;         // задержка основного цикла в миллисекундах
        ракета: структура        // параметры ракеты
            пределы: Размер;     // пределы перемещения ракеты (в точках)
            максСкор: дробное;   // макс. скорость ракеты (в точках за такт)
            ускорение: дробное;  // приращение скорости (в точках за такт)
            максФакел: целое;    // макс. длина факела центрального двигателя (длина массива изображений факела)
            средФакел: дробное;  // средняя длина факела центрального двигателя (ракета не ускоряется ни вверх, ни вниз)
            корп: структура      // габариты корпуса для расчёта столкновения с астероидом
                вл: Точка;       // верхний левый угол
                пн: Точка;       // правый нижний угол
            окончание;
        окончание;
        выстрел: структура       // параметры выстрелов
            скор: дробное;       // скорость пули (в точках за такт)
            отдача: дробное;     // приращение скорости ракеты при выстреле
            задержка: целое;     // скорострельность -- мин. кол-во тактов между выстрелами
            урон: целое;         // каждый попавший выстрел вычитает это число из прочности астероида
        окончание;
        камень: структура        // параметры астероидов
            макс: целое;         // количество изображений астероидов
            кво: целое;          // начальное количество астероидов, одновременно летящих на экране
            максПоз: дробное;    // максимальная начальная вертикальная координата астероида
            минСкор: ДрТочка;    // минимальная скорость
            максСкор: ДрТочка;   // максимальная скорость
            вер: массив целых;   // вероятность появления астероидов (длина массива должна равняться камень.макс)
        окончание;
    окончание;

константа
    // Значения глобальных параметров
    парам: Параметры = {
        экран = {г = 1024, в = 768},
        скорФона = 1,
        задержка = 10,
        ракета = {
            пределы = {г = 899, в = 530},
            максСкор = 5,
            ускорение = 0.1,
            максФакел = 6,
            средФакел = 2,
            корп = {
                вл = {г = 30, в = 15}, 
                пн = {г = 95, в = 185}}},
        выстрел = {
            скор = -10,
            отдача = 0.9,
            задержка = 30,
            урон = 2000},
        камень = {
            макс = 9,
            кво = 10,
            максПоз = 150,
            минСкор = {г = -2, в = 0.5},
            максСкор = {г = 2, в = 4},
            вер = [1, 1, 3, 4, 6, 8, 10, 12, 15]}};
    
константы
    // Коды клавиш
    клВлево = 37;
    клВверх = 38;
    клВправо = 39;
    клВниз = 40;
    клВыход = 27;
    клПробел = 32;

переменная
    // Словарь состояний клавиш: ДА, если клавиша нажата.
    // Отслеживается состояние только тех клавиш, которые изначально есть в словаре.
    клав: словарь логических ключ целое = {
        клВлево: нет, клВверх: нет, клВправо: нет, клВниз: нет, клВыход: нет, клПробел: нет};

типы
    // Состояние ракеты
    Ракета = структура
        поз: ДрТочка;    // координаты верхнего левого угла ракеты (в точках)
        скор: ДрТочка;   // скорость ракеты (в точках за такт)
        факел: целое;    // длина факела центрального двигателя -- индекс изображения в массиве
        лдв: логическое; // левый двигатель включён
        пдв: логическое; // правый двигатель включён
        выстрел: целое;  // обратный отсчёт тактов до следующего выстрела
    окончание;
    
    // Состояние летящей пули
    Пуля/Пуль = структура
        поз: ДрТочка;
        скор: дробное;
    окончание;
    
    // Состояние летящего астероида
    Камень/Камней = структура
        изо: объект;   // изображение
        поз: ДрТочка;  // координаты верхнего левого угла (в точках)
        скор: ДрТочка; // скорость движения (в точках за такт)
        разм: Размер;  // размер камня
        прочн: целое;  // остаточная прочность (уменьшается при попадании выстрелов)
    окончание;

переменные
    // Картинки, используемые в игре
    изо: структура
        фон: объект;          // Фоновое изображение
        ркт: объект;          // Изображение ракеты
        цдв: массив объектов; // Изображения факела центрального двигателя
        лдв, пдв: объекты;    // Изображения факелов левого и правого двигателей
        кмн: массив объектов; // Изображения астероидов
    окончание;
    
    // Окно графического вывода
    окно: объект;
    
    // Смещение фоновой картинки
    фон: целое;
    
    // Ракета
    ркт: Ракета;
    
    // Летящие пули
    пули: массив Пуль;
    
    // Летящие астероиды
    камни: массив Камней;
    
    // Набранные очки
    очки: целое;

// Загружает картинки, используемые в игре
процедура загрузитьИзо();
переменные
    к: целое;
    кат: строка;
начало
    кат := файлПуть(файлВыполняемый()) ++ "/астероиды/";
    изо.фон := грИзоЗагрузить(кат ++ "фон.jpg");
    изо.ркт := грИзоЗагрузить(кат ++ "ракета.png");
    изо.лдв := грИзоЗагрузить(кат ++ "левый.png");
    изо.пдв := грИзоЗагрузить(кат ++ "правый.png");
    длина(изо.цдв, парам.ракета.максФакел);
    для к от 0 до парам.ракета.максФакел-1 цикл
        изо.цдв[к] := грИзоЗагрузить(кат ++ "центр" ++ строка(к+1) ++ ".png");
    длина(изо.кмн, парам.камень.макс);
    для к от 0 до парам.камень.макс-1 цикл
        изо.кмн[к] := грИзоЗагрузить(кат ++ "камень" ++ строка(к+1) ++ ".png");
окончание;

// Уничтожает загруженные картинки
процедура уничтожитьИзо();
переменная
    к: целое;
начало
    уничтожить(изо.фон);
    уничтожить(изо.ркт);
    уничтожить(изо.лдв);
    уничтожить(изо.пдв);
    для каждого к из изо.цдв цикл уничтожить(изо.цдв[к]);
    для каждого к из изо.кмн цикл уничтожить(изо.кмн[к]);
окончание;

// Возвращает индекс изображения нового астероида, исходя из вероятности его появления
функция какойКамень(): целое;
переменные
    сум, слч, к: целое;
начало
    для каждого к из парам.камень.вер цикл сум += парам.камень.вер[к];
    слч := случайное(сум);
    для каждого к из парам.камень.вер обратный цикл начало
        сум -= парам.камень.вер[к];
        если слч > сум то вернуть к;
    конец;
окончание;

// Создаёт случайный астероид в указанных координатах
процедура создатьКамень(к: целое; г, в: дробное);
переменная
    ц: целое;
начало
    камни[к].поз.г := г;
    камни[к].поз.в := в;
    ц := целое((парам.камень.максСкор.в-парам.камень.минСкор.в)*100);
    камни[к].скор.в := парам.камень.минСкор.в + случайное(ц)/100;
    ц := целое((парам.камень.максСкор.г-парам.камень.минСкор.г)*100);
    камни[к].скор.г := парам.камень.минСкор.г + случайное(ц)/100;
    камни[к].изо := изо.кмн[какойКамень()];
    камни[к].разм := грРазмер(камни[к].изо);
    камни[к].прочн := камни[к].разм.г * камни[к].разм.в;
окончание;

// Уничтожает астероид
процедура уничтожитьКамень(к: целое);
начало
    камни[к].изо := пусто;
окончание;

// Создаёт случайные астероиды в начале игры
процедура создатьКамни();
переменная
    к, шаг, г, в: целое;
начало
    длина(камни, парам.камень.кво);
    шаг := парам.экран.г \ (парам.камень.кво+1);
    для к от 0 до парам.камень.кво-1 цикл начало
        г := (к+1)*шаг - случайное(шаг);
        в := случайное(целое(парам.камень.максПоз));
        создатьКамень(к, г, в);
    конец;
окончание;

// Всё, что нужно сделать перед началом игры
процедура инициализация();
начало
    окно := грОкно("Астероиды");
    грРазмер(окно, парам.экран.г, парам.экран.в);
    загрузитьИзо();
    сбтЗаказать(окно, сбтКлНаж | сбтКлОтп);
    ркт.поз.г := парам.ракета.пределы.г\2;
    ркт.поз.в := парам.ракета.пределы.в;
конец;

// Всё, что нужно сделать по окончании игры
процедура завершение();
начало
    уничтожитьИзо();
    уничтожить(окно);
окончание;

// Возвращает ДА, если прямоугольники пересекаются
функция пересекает(п11, п12: ДрТочка; п21, п22: ДрТочка): логическое;
начало
    вернуть п11.г < п22.г и п12.г >= п21.г и п11.в < п22.в и п12.в >= п21.в;
окончание;

// Рассчитывает положение и состояние ракеты на каждой итерации основного цикла
процедура расчётРакеты();
переменная
    напр: Точка;
    влр, пнр, влк, пнк: ДрТочка;
    к: целое;
начало
    // направление ускорения/замедления
    если клав[клВправо] то напр.г += 1;
    если клав[клВлево] то напр.г -= 1;
    если клав[клВниз] то напр.в += 1;
    если клав[клВверх] то напр.в -= 1;
    // ускорение/замедление
    если напр.г > 0 то начало
        ркт.скор.г += парам.ракета.ускорение;
        если ркт.скор.г > парам.ракета.максСкор то ркт.скор.г := парам.ракета.максСкор;
    конец иначе если напр.г < 0 то начало
        ркт.скор.г -= парам.ракета.ускорение;
        если ркт.скор.г < -парам.ракета.максСкор то ркт.скор.г := -парам.ракета.максСкор;
    конец иначе если ркт.скор.г > 0 то начало
        ркт.скор.г -= парам.ракета.ускорение;
        если ркт.скор.г < 0 то ркт.скор.г := 0;
    конец иначе если ркт.скор.г < 0 то начало
        ркт.скор.г += парам.ракета.ускорение;
        если ркт.скор.г > 0 то ркт.скор.г := 0;
    конец;
    если напр.в > 0 то начало
        ркт.скор.в += парам.ракета.ускорение;
        если ркт.скор.в > парам.ракета.максСкор то ркт.скор.в := парам.ракета.максСкор;
    конец иначе если напр.в < 0 то начало
        ркт.скор.в -= парам.ракета.ускорение;
        если ркт.скор.в < -парам.ракета.максСкор то ркт.скор.в := -парам.ракета.максСкор;
    конец иначе если ркт.скор.в > 0 то начало
        ркт.скор.в -= парам.ракета.ускорение;
        если ркт.скор.в < 0 то ркт.скор.в := 0;
    конец иначе если ркт.скор.в < 0 то начало
        ркт.скор.в += парам.ракета.ускорение;
        если ркт.скор.в > 0 то ркт.скор.в := 0;
    конец;
    // положение ракеты
    ркт.поз.г += ркт.скор.г;
    если ркт.поз.г < 0 то ркт.поз.г := 0
    иначе если ркт.поз.г > парам.ракета.пределы.г то ркт.поз.г := парам.ракета.пределы.г;
    ркт.поз.в += ркт.скор.в;
    если ркт.поз.в < 0 то ркт.поз.в := 0
    иначе если ркт.поз.в > парам.ракета.пределы.в то ркт.поз.в := парам.ракета.пределы.в;
    // факелы двигателей
    ркт.пдв := напр.г < 0;
    ркт.лдв := напр.г > 0;
    выбор напр.в из
       -1: если ркт.факел < парам.ракета.максФакел то ркт.факел += 1;
        1: если ркт.факел > 0 то ркт.факел -= 1;
        иначе 
            если ркт.факел < парам.ракета.средФакел то ркт.факел += 1
            иначе если ркт.факел > парам.ракета.средФакел то ркт.факел -= 1;
    конец;
    // скорострельность
    если ркт.выстрел > 0 то ркт.выстрел -= 1;
    // столкновение с астероидом
    влр := {
        г = ркт.поз.г + парам.ракета.корп.вл.г,
        в = ркт.поз.в + парам.ракета.корп.вл.в};
    пнр := {
        г = ркт.поз.г + парам.ракета.корп.пн.г,
        в = ркт.поз.в + парам.ракета.корп.пн.в};
    для каждого к из камни цикл начало
        если камни[к].изо = пусто то продолжить;
        влк := камни[к].поз;
        пнк := {
            г = камни[к].поз.г + камни[к].разм.г,
            в = камни[к].поз.в + камни[к].разм.в};
        если пересекает(влр, пнр, влк, пнк) то завершить;
    конец;
окончание;

// Рассчитывает положение летящих пуль на каждой итерации основного цикла,
// отслеживает попадание пуль в астероиды
процедура расчётПуль();
переменная
    к, л: целое;
    нп, кп1, кп2: ДрТочка;
    попадание: логическое;
начало
    для каждого к из пули обратный цикл начало
        попадание := нет;
        нп := {г = пули[к].поз.г, в = пули[к].поз.в + парам.выстрел.скор};
        для каждого л из камни цикл начало
            если камни[л].изо = пусто то продолжить;
            кп1 := камни[л].поз;
            кп2 := {г = камни[л].поз.г+камни[л].разм.г, в = камни[л].поз.в+камни[л].разм.в};
            если пересекает(нп, пули[к].поз, кп1, кп2) то начало
                камни[л].прочн -= парам.выстрел.урон;
                если камни[л].прочн <= 0 то начало
                    очки += камни[л].разм.г * камни[л].разм.в;
                    уничтожитьКамень(л);
                конец;
                удалить(пули, к);
                попадание := да;
                прервать;
            конец;
        конец;
        если попадание то продолжить;
        пули[к].поз := нп;
        если пули[к].поз.в < 0 то удалить(пули, к);
    конец;
окончание;

// Рассчитывает положение летящих астероидов на каждой итерации основного цикла
процедура расчётКамней();
переменная
    к: целое;
    г, в: дробное;
начало
    для каждого к из камни цикл начало
        если камни[к].изо = пусто
        или камни[к].поз.г > парам.экран.г или камни[к].поз.г + камни[к].разм.г <= 0
        или камни[к].поз.в > парам.экран.в или камни[к].поз.в + камни[к].разм.в <= 0 то начало
            создатьКамень(к, 0, 0);
            камни[к].поз.г := случайное(парам.экран.г-камни[к].разм.г);
            камни[к].поз.в := -камни[к].разм.в+1;
        конец иначе начало
            камни[к].поз.г += камни[к].скор.г;
            камни[к].поз.в += камни[к].скор.в;
        конец;
    конец;
окончание;

// рассчитывает всю игровую механику
процедура рассчитать();
начало
    фон += парам.скорФона;
    если фон >= парам.экран.в то фон := 0;
    расчётКамней();
    расчётРакеты();
    расчётПуль();
окончание;

// Проверяет возможность выстрела и создаёт летящие пули
процедура выстрелить();
переменная
    п: Пуля;
начало
    если ркт.выстрел > 0 то вернуться;
    п.скор := парам.выстрел.скор;
    п.поз.в := ркт.поз.в + 100;
    п.поз.г := ркт.поз.г + 10;
    добавить(пули, п);
    п.поз.г := ркт.поз.г + 114;
    добавить(пули, п);
    ркт.выстрел := парам.выстрел.задержка;
    ркт.скор.в += парам.выстрел.отдача;
    если ркт.скор.в < -парам.ракета.максСкор то ркт.скор.в := -парам.ракета.максСкор
    иначе если ркт.скор.в > парам.ракета.максСкор то ркт.скор.в := парам.ракета.максСкор;
окончание;

// Выводит ракету на переданный холст
процедура вывестиРакету(холст: объект);
переменные
    г, в: целое;
начало
    г := округл(ркт.поз.г);
    в := округл(ркт.поз.в);
    грИзоВывести(холст, г, в, изо.ркт);
    если ркт.факел > 0 то грИзоВывести(холст, г+39, в+184, изо.цдв[ркт.факел-1]);
    если ркт.лдв то грИзоВывести(холст, г-7, в+168, изо.лдв);
    если ркт.пдв то грИзоВывести(холст, г+109, в+168, изо.пдв);
окончание;

// Выводит летящие пули на переданный холст
процедура вывестиПули(холст: объект);
переменная
    к, г, в: целое;
начало
    грПеро(холст, $FFFFFF, 3, грспЛиния);
    для каждого к из пули цикл начало
        г := округл(пули[к].поз.г);
        в := округл(пули[к].поз.в);
        грОтрезок(холст, г, в, г, в+3);
    конец;
окончание;

// Выводит астероиды на переданный холст
процедура вывестиКамни(холст: объект);
переменная
    к: целое;
начало
    для каждого к из камни цикл
        если камни[к].изо <> пусто то
            грИзоВывести(холст, целое(камни[к].поз.г), целое(камни[к].поз.в), камни[к].изо);
окончание;

// Выводит набранные очки и подсказку
процедура вывестиТекст(холст: объект);
переменная
    р: Размер;
    с: строка;
начало
    грШрифт(холст, $FFFFFF);
    грКисть(холст, 0, грскПусто);
    грПеро(холст, 0, 0, грскПусто);
    с := "Счёт: " ++ строка(очки);
    р := грРазмерТекста(холст, с);
    грТекст(холст, парам.экран.г - р.г - 10, парам.экран.в - р.в - 10, с);
    грТекст(холст, 10, парам.экран.в - р.в - 10, "Управление: стрелки, пробел, ESC");
окончание;

// Рисует экран на каждой итерации основного цикла
процедура отрисовать();
начало
    грНачать(окно);
    начало
        грИзоВывести(окно, 0, фон-парам.экран.в, изо.фон);
        грИзоВывести(окно, 0, фон, изо.фон);
        вывестиКамни(окно);
        вывестиРакету(окно);
        вывестиПули(окно);
        вывестиТекст(окно);
    напоследок
        грЗакончить(окно);
    конец;
окончание;

// Читает события клавиатуры и устанавливает состояния клавиш
процедура обработатьСобытия();
переменная
    сбт: Событие;
начало
    пока сбтЕсть(окно) цикл начало
        сбт := сбтЗабрать(окно);
        выбор сбт.что из
            сбтКлНаж: если есть(клав[сбт.код]) то клав[сбт.код] := да;
            сбтКлОтп: если есть(клав[сбт.код]) то клав[сбт.код] := нет;
        конец;
    конец;
окончание;

// Основной блок программы
константа
    сек = момент(1/68400);
переменная
    нач, врм: момент;
начало
    вывести("Эта программа работает только в отладочной среде Клаус.", НС);
    нач := сейчас();
    инициализация();
    создатьКамни();
    цикл начало
        обработатьСобытия();
        если клав[клПробел] то выстрелить();
        рассчитать();
        отрисовать();
        пауза(парам.задержка);
        врм := сейчас();
        если врм-нач >= сек то начало
            очки += 100;
            нач := врм;
        конец;
    конец пока не клав[клВыход];
напоследок
    завершение();
    вывести("Вы набрали " ++ строка(очки) ++ " очков.", НС);
    вывести("Игра закончена.", НС);
окончание.
