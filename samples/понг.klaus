программа Понг(арг: массив строк);

используется Терминал;

типы
    тРакетка = структура
        поз: целое;
        скор: целое;
    конец;
    тМяч = структура 
        гПоз, гСкор: целое;
        вПоз, вСкор: целое;
    конец;
    тСчёт = структура
        лев: целое;
        прав: целое;
    конец;

константы
    симвЛР = №2590;
    симвПР = №258ц;
    симвМяч = 'o';

переменные
    ширинаПоля: целое = 80;
    высотаПоля: целое = 25;
    максСчёт: целое = 10;
    скорость: целое = 3;
    задержка: целое = 70;
    строкаПоля: строка;
    верхняяГраница: строка;
    нижняяГраница: строка;
    лр: тРакетка;
    пр: тРакетка;
    мяч: тМяч;
    счёт: тСчёт;
    фаза: целое;
    паузаФазы: целое;
    
константы
    фазаЗаставка = 0;
    фазаИгра = 1;
    фазаОчкоПравому = 2;
    фазаОчкоЛевому = 3;
    фазаВыигралПравый = 4;
    фазаВыигралЛевый = 5;

функция строкаИз(симв: символ; кво: целое): строка;
переменные
    к: целое;
    с: строка = "";
начало
    для к от 0 до кво-1 цикл с := с ++ симв;
    вернуть с;
окончание;

процедура мячНаИсходную(вв мяч: тМяч);
начало
    мяч.гПоз := ширинаПоля\2;
    мяч.вПоз := случайное(высотаПоля-2)+1;
    если случайное(2) = 1 то мяч.гСкор := 1 иначе мяч.гСкор := -1;
    если случайное(2) = 1 то мяч.вСкор := 1 иначе мяч.вСкор := -1;
окончание;

процедура ракеткаНаИсходную(вв р: тРакетка);
начало
    р.поз := высотаПоля\2;
    р.скор := 0;
окончание;

процедура инициализация();
начало
    // аргументы
    если длина(арг) >= 1 то скорость := целое(арг[0]);
    если длина(арг) >= 2 то максСчёт := целое(арг[1]);
    если длина(арг) >= 3 то ширинаПоля := целое(арг[2]);
    если длина(арг) >= 4 то высотаПоля := целое(арг[3]);
    если ширинаПоля < 70 то ширинаПоля := 70
    иначе если ширинаПоля > 160 то ширинаПоля := 160;
    если высотаПоля < 20 то высотаПоля := 20
    иначе если высотаПоля > 50 то высотаПоля := 50;
    выбор скорость из
        1: задержка := 200;
        2: задержка := 120;
        3: задержка := 70;
        4: задержка := 40;
        5: задержка := 25;
        6: задержка := 14;
        7: задержка := 8;
        8: задержка := 5;
        9: задержка := 3;
        иначе задержка := 1;
    конец;
    // строки для отрисовки поля
    верхняяГраница := №250ф ++ строкаИз(№2501, ширинаПоля) ++ №2513;
    нижняяГраница := №2517 ++ строкаИз(№2501, ширинаПоля) ++ №251б;
    строкаПоля := строкаИз(' ', ширинаПоля+2);
    // инициализация состояния
    ракеткаНаИсходную(лр);
    ракеткаНаИсходную(пр);
    мячНаИсходную(мяч);
    // подготовка терминала
    режимТерминала(идСтдВывод, трСквозной);
    размерЭкрана(ширинаПоля+4, высотаПоля+5);
    очиститьЭкран();
    скрытьКурсор();
окончание;

процедура отрисовать(г, в: целое);
переменные
    К: целое;
    стр: строка;
начало
    // заставка
    если фаза = фазаЗаставка то начало
        курсор(0, 2);
        цветШрифта(цвет256(0, 192, 0));
        стр := строкаИз(' ', ширинаПоля\2-13);
        вывести(стр ++ "▒▒▒▒▒▒▒  ▒▒▒▒▒  ▒▒   ▒▒ ▒▒▒▒▒▒", НС);
        вывести(стр ++ "▒▒   ▒▒ ▒▒   ▒▒ ▒▒   ▒▒ ▒▒    ", НС);
        вывести(стр ++ "▒▒   ▒▒ ▒▒   ▒▒ ▒▒▒▒▒▒▒ ▒▒    ", НС);
        вывести(стр ++ "▒▒   ▒▒ ▒▒   ▒▒ ▒▒   ▒▒ ▒▒    ", НС);
        вывести(стр ++ "▒▒   ▒▒  ▒▒▒▒▒  ▒▒   ▒▒ ▒▒    ", НС, НС, НС);
        сброситьАтрибуты();
        вывести("  Параметры командной строки:", НС);
        вывести("    klaus понг.клаус <скорость> <макс-счёт> <ширина-поля> <высота-поля>", НС, НС);
        вывести("  Управление:", НС);
        вывести("    A, Z -- левая ракетка;", НС);
        вывести("    K, M -- правая ракетка;", НС);
        вывести("    повторное нажатие останавливает ракетку.", НС, НС);
        вывести("    Q -- закончить игру.", НС, НС);
        вывести("  Нажмите ПРОБЕЛ для начала игры или Q для выхода...", НС);
        вернуться;
    конец;
    // поле
    курсор(г, в);
    вывести(верхняяГраница);
    для К от 0 до высотаПоля-1 цикл начало
        курсор(г, в+К+1);
        стр := строкаПоля;
        если К >= пр.поз-1 && К <= пр.поз+1 то 
            заменить(стр, ширинаПоля+1, 1, симвПР);
        если К = мяч.вПоз то
            заменить(стр, мяч.гПоз+1, 1, симвМяч);
        если К >= лр.поз-1 && К <= лр.поз+1 то 
            заменить(стр, 0, 1, симвЛР);
        вывести(стр);
    конец;
    курсор(г, в+высотаПоля+1);
    вывести(нижняяГраница);
    // счёт
    выбор фаза из
        фазаВыигралЛевый: стр := "Terrorists Win!";
        фазаВыигралПравый: стр := "Counter-Terrorists Win!";
        иначе стр := формат("%2ц : %2ц", счёт.лев, счёт.прав);
    конец;
    курсор(г+ширинаПоля\2-след(стр, 0, максЦелое)\2+1, в+высотаПоля+2);
    очиститьСтроку(0);
    вывести(стр);
окончание;

процедура войтиВФазу(ф: целое);
начало
    фаза := ф;
    если фаза <> фазаИгра то начало
        паузаФазы := 5000\задержка;
        ракеткаНаИсходную(лр);
        ракеткаНаИсходную(пр);
        мячНаИсходную(мяч);
    конец;
    если фаза = фазаЗаставка то паузаФазы := 30000\задержка;
окончание;

процедура рассчитатьМяч(вв мяч: тМяч);
начало
    мяч.вПоз += мяч.вСкор;
    если мяч.вПоз < 0 то начало
        мяч.вПоз := -мяч.вПоз;
        мяч.вСкор := -мяч.вСкор;
    конец иначе если мяч.вПоз >= высотаПоля то начало
        мяч.вПоз := 2*(высотаПоля-1)-мяч.вПоз;
        мяч.вСкор := -мяч.вСкор;
    конец;
    мяч.гПоз += мяч.гСкор;
    если мяч.гПоз < 0 то начало
        если мяч.вПоз < лр.поз-1 || мяч.вПоз > лр.поз+1 то начало
            счёт.прав += 1;
            если счёт.прав < максСчёт то войтиВФазу(фазаОчкоПравому)
            иначе войтиВФазу(фазаВыигралПравый);
        конец иначе начало
            мяч.гПоз := -мяч.гПоз;
            мяч.гСкор := -мяч.гСкор;
        конец;
    конец иначе если мяч.гПоз >= ширинаПоля то начало
        если мяч.вПоз < пр.поз-1 || мяч.вПоз > пр.поз+1 то начало
            счёт.лев += 1;
            если счёт.лев < максСчёт то войтиВФазу(фазаОчкоЛевому)
            иначе войтиВФазу(фазаВыигралЛевый);
        конец иначе начало
            мяч.гПоз := 2*(ширинаПоля-1)-мяч.гПоз;
            мяч.гСкор := -мяч.гСкор;
        конец;
    конец;
окончание;

процедура рассчитатьРакетку(вв р: тРакетка);
начало
    р.поз += р.скор;
    если р.поз < 1 то р.поз := 1
    иначе если р.поз > высотаПоля-2 то р.поз := высотаПоля-2;
окончание;

процедура обработатьКлавиши();
переменная
    с: символ;
начало
    пока естьСимвол() цикл начало
        с := прочестьСимвол();
        выбор с из
            'a', 'A', 'ф', 'Ф': если фаза = фазаИгра то начало
                если лр.скор < 0 то лр.скор := 0
                иначе лр.скор := -1;
            конец;
            'z', 'Z', 'я', 'Я': если фаза = фазаИгра то начало
                если лр.скор > 0 то лр.скор := 0
                иначе лр.скор := 1;
            конец;
            'k', 'K', 'л', 'Л': если фаза = фазаИгра то начало
                если пр.скор < 0 то пр.скор := 0
                иначе пр.скор := -1;
            конец;
            'm', 'M', 'ь', 'Ь': если фаза = фазаИгра то начало
                если пр.скор > 0 то пр.скор := 0
                иначе пр.скор := 1;
            конец;
            №20: если фаза <> фазаИгра то паузаФазы := 0;
            'q', 'Q', 'й', 'Й': завершить(0);
        конец;
    конец;
окончание;

начало
    инициализация();
    войтиВФазу(фазаЗаставка);
    пока да цикл начало
        отрисовать(1, 1);
        обработатьКлавиши();
        если фаза = фазаИгра то начало
            рассчитатьРакетку(лр);
            рассчитатьРакетку(пр);
            рассчитатьМяч(мяч);
        конец иначе начало
            паузаФазы -= 1;
            если паузаФазы <= 0 то начало
                если фаза = фазаВыигралЛевый || фаза = фазаВыигралПравый то начало
                    счёт.лев := 0;
                    счёт.прав := 0;
                конец;
                войтиВФазу(фазаИгра);
            конец;
        конец;
        пауза(задержка);
    конец;
напоследок
    режимТерминала(идСтдВывод, трКанон);
    показатьКурсор();
    вывести(НС);
окончание.
