программа ГрафическийВывод;
переменная
    окно, изо, экр: объект;
    к, г: целое;
    тчк: МассивТочек;
    р: Размер;
начало
    вывести("Эта программа работает только в отладочной среде Клаус.", НС);
    окно := грОткрытьОкно("Графическое окно");
    грРазмер(окно, 1024, 768);
    грПеро(окно, $0000ФФ, 2, грсп2ТочкиТире);
    грКисть(окно, $7Ф0000);
    грНачать(окно);
    начало
        для к от 0 до 90 цикл 
            грКруг(окно, 100+к*10, 100, 70);
        грПеро(окно, $FFFFFF, 3, грспЛиния);
        грКисть(окно, $7F7F7F, грскПусто);
        грЭллипс(окно, 20, 150, 220, 200);
        грКисть(окно, $7F7F7F, грскПусто);
        грДуга(окно, 100, 300, 200, 600, Пи/4, 2*Пи-0.2);
        грКисть(окно, $707070, грскЦвет);
        грСегмент(окно, 450, 300, 650, 600, Пи/4, Пи-1);
        грСектор(окно, 450, 450, 550, 550, Пи/4, Пи/2);
        грПеро(окно, $FFFFFF, 1);
        грОтрезок(окно, 30, 70, 300, 600);
        грПрямоугольник(окно, 300, 400, 400, 450);
        грПрямоугольник(окно, 300, 500, 400, 550, 20, 10);
        длина(тчк, 5);
        тчк[0].г := 50;
        тчк[0].в := 150;
        тчк[1].г := 150;
        тчк[1].в := 100;
        тчк[2].г := 350;
        тчк[2].в := 250;
        тчк[3].г := 220;
        тчк[3].в := 400;
        тчк[4].г := 400;
        тчк[4].в := 300;
        грЛоманая(окно, тчк);
        длина(тчк, 6);
        тчк[0].г := 650;
        тчк[0].в := 150;
        тчк[1].г := 750;
        тчк[1].в := 100;
        тчк[2].г := 950;
        тчк[2].в := 250;
        тчк[3].г := 1000;
        тчк[3].в := 400;
        тчк[4].г := 900;
        тчк[4].в := 500;
        тчк[5].г := 700;
        тчк[5].в := 300;
        грМногоугольник(окно, тчк);
    напоследок
        грЗакончить(окно);
    конец;
    для к от 0 до 5000 цикл
        грТочка(окно, случайное(1024), случайное(768), $00FF00);
    изо := грИзоЗагрузить(файлПолныйПуть(файлПуть(файлВыполняемый())) ++ "test.png");
    р := грРазмер(изо);
    экр := грИзоСоздать(окно, 0, 0, 1024, 768);
    г := -р.г;
    пока г+р.г < 1024 цикл начало
        грНачать(окно);
        начало
            грИзоВывести(окно, 0, 0, экр);
            грИзоВывести(окно, г, 200, изо);
            г += 1;
            пауза(2);
        напоследок
            грЗакончить(окно);
        конец;
    конец;
    пока г > 0 цикл начало
        грНачать(окно);
        начало
            грИзоВывести(окно, 0, 0, экр);
            грИзоВывести(окно, г, 200, изо);
            г -= 1;
            пауза(2);
        напоследок
            грЗакончить(окно);
        конец;
    конец;
    грУничтожить(изо);
    грУничтожить(экр);
    грКисть(окно, 0, грскПусто);
    грШрифт(окно, $FFFFFF);
    р := грТекст(окно, 50, 650, "Перейдите на закладку ""Консоль"" и нажмите Enter для завершения...");
    грПеро(окно, $ФФФФФФ, 1, грспТочка);
    грПрямоугольник(окно, 50, 650, 50+р.г, 650+р.в);
    вывести("Нажмите Enter для завершения...");
    ввести();
    грУничтожить(окно);
окончание.
