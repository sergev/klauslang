программа ГрафическийВывод;
переменная
    окно, изо, экр: объект;
    к, г, в, гс, вс: целое;
    тчк: МассивТочек;
    р: Размер;
    сбт: Событие;
начало
    вывести("Эта программа работает только в отладочной среде Клаус.", НС);
    окно := грОкно("Графическое окно");
    грРазмер(окно, 1024, 768);
    грПеро(окно, $0000ФФ, 2, грсп2ТочкиТире);
    грКисть(окно, $7Ф0000);
    грНачать(окно);
    начало
        для к от 0 до 90 цикл 
            грКруг(окно, 100+к*10, 100, 70);
        грПеро(окно, $FFFFFF, 3, грспЛиния);
        грКисть(окно, $7F7F7F, грскПусто);
        грЭллипс(окно, 20, 150, 220, 200);
        грКисть(окно, $7F7F7F, грскПусто);
        грДуга(окно, 100, 300, 200, 600, Пи/4, 2*Пи-0.2);
        грКисть(окно, $707070, грскЦвет);
        грСегмент(окно, 450, 300, 650, 600, Пи/4, Пи-1);
        грСектор(окно, 450, 450, 550, 550, Пи/4, Пи/2);
        грПеро(окно, $FFFFFF, 1);
        грОтрезок(окно, 30, 70, 300, 600);
        грПрямоугольник(окно, 300, 400, 400, 450);
        грПрямоугольник(окно, 300, 500, 400, 550, 20, 10);
        тчк := [{г=50, в=150}, {г=150, в=100}, {г=350, в=250}, {г=220, в=400}, {г=400, в=300}];
        грЛоманая(окно, тчк);
        тчк := [{г=650, в=150}, {г=750, в=100}, {г=950, в=250}, {г=1000, в=400}, {г=900, в=500}, {г=700, в=300}];
        грМногоугольник(окно, тчк);
    напоследок
        грЗакончить(окно);
    конец;
    для к от 0 до 5000 цикл
        грТочка(окно, случайное(1024), случайное(768), $00FF00);
    грКисть(окно, 0, грскПусто);
    грШрифт(окно, $FFFFFF, грсшЖирный | грсшКурсив);
    р := грТекст(окно, 50, 650, "Управляйте стрелками, нажмите ESC для завершения...");
    грПеро(окно, $ФФФФФФ, 1, грспТочка);
    грПрямоугольник(окно, 50, 650, 50+р.г, 650+р.в);
    изо := грИзоЗагрузить(файлПуть(файлВыполняемый()) ++ "test.png");
    р := грРазмер(изо);
    экр := грИзоСоздать(окно, 0, 0, 1024, 768);
    г := 512-р.г\2;
    в := 384-р.в\2;
    сбтЗаказать(окно, сбтКлНаж | сбтКлОтп);
    пока ДА цикл начало
        грНачать(окно);
        начало
            грИзоВывести(окно, 0, 0, экр);
            грИзоВывести(окно, г, в, изо);
            пауза(2);
        напоследок
            грЗакончить(окно);
        конец;
        г += гс;
        в += вс;
        если сбтЕсть(окно) то начало
            сбт := сбтЗабрать(окно);
            выбор сбт.код из
                37: если сбт.что = сбтКлНаж то гс := -1 иначе гс := 0; // влево
                38: если сбт.что = сбтКлНаж то вс := -1 иначе вс := 0; // вверх
                39: если сбт.что = сбтКлНаж то гс := 1 иначе гс := 0;  // вправо
                40: если сбт.что = сбтКлНаж то вс := 1 иначе вс := 0;  // вниз
                27: если сбт.что = сбтКлНаж то прервать;               // esc
            конец;
        конец;
    конец;
    вывести("Программа завершена.");
напоследок
    уничтожить(изо);
    уничтожить(экр);
    уничтожить(окно);
окончание.
