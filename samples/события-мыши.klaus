программа СобытияМыши;

используются
    События, Графика;

переменная
    окно: объект;
    сбт: Событие;
    фон: целое = 0;
    цвет: целое = $А0А0А0;
    г, в: целое = 100;
    р: целое = 20;
    рзм: Размер;
    
процедура отрисовать();
начало
    грНачать(окно);
    начало
        грКисть(окно, фон);
        грПрямоугольник(окно, 0, 0, рзм.г, рзм.в);
        грКисть(окно, цвет);
        грПеро(окно, $ФФФФФФ, 2, грспЛиния);
        грКруг(окно, г, в, р);
    напоследок
        грЗакончить(окно);
    конец;
окончание;

процедура обновитьЦвет(мг, мв: целое; инфо: целое);
начало
    если ((мг-г)^2 + (мв-в)^2)^(1/2) < р то начало
        если инфо && сскЛКМ > 0 то цвет := 0
        иначе цвет := $ФФФФФФ;
    конец иначе
        цвет := $А0А0А0;
окончание;
    
начало
    окно := грОкно("Тест мыши");
    рзм := грРазмер(окно);
    г := рзм.г\2;
    в := рзм.в\2;
    отрисовать();
    сбтЗаказать(окно, сбтКлНаж || сбтМшВх || сбтМшВых || сбтМшДвг || сбтМшНаж || сбтМшОтп || сбтМшКлс);
    пока ДА цикл начало
        если сбтЕсть(окно) то начало
            сбт := сбтЗабрать(окно);
            выбор сбт.что из
                сбтКлНаж: если сбт.код = 27 то завершить;
                сбтМшВх: фон := $А07000;
                сбтМшВых: фон := 0;
                сбтМшДвг, сбтМшНаж, сбтМшОтп: обновитьЦвет(сбт.г, сбт.в, сбт.инфо);
                сбтМшКлс: начало
                    р += сбт.код\60;
                    обновитьЦвет(сбт.г, сбт.в, сбт.инфо);
                конец;
            конец;
        конец;
        отрисовать();
        пауза(3);
    конец;
напоследок
    уничтожить(окно);
окончание.
