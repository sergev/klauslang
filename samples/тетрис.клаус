программа Тетрис(арг: массив строк);

константы
    стрБлок = ""№2588№2588;
    смвЛвРамка = №2590;
    смвПрРамка = №258Ц;
    смвНжРамка = №2580;
    
    клНет = 0;
    клВлево = 1;
    клВправо = 2;
    клПоЧС = 3;
    клПрЧС = 4;
    клСбросить = 5;
    клВыход = 6;
    клПауза = 7;
    
    всегоФигур = 7;

типы
    ИгровоеПоле = структура
        шир: целое;
        выс: целое;
        клетки: массив массивов целых;
    окончание;
    
    Точка/Точек = структура
        г, в: целые;
    окончание;
    
    Фигура/Фигуры/Фигур = структура
        ид: целое;
        г, в: целые;
        клетки: массив Точек;
    окончание;

переменные
    цветРамки: целое = 7;
    цвета: массив целых; // цвет для каждого типа фигуры от 1 до 5, 0 -- пустая клетка
    поле: ИгровоеПоле;
    клавиши: словарь целых ключ строка;
    скоростьСпуска: целое = 20;
    скоростьПадения: целое = 0;
    очки: целое;
    заставка: логическое;

функция тчк(г, в: целые): Точка;
переменная
    т: Точка;
начало
    т.г := г;
    т.в := в;
    вернуть т;
окончание;

процедура создатьПоле();
переменные
    в, г: целые;
начало
    длина(поле.клетки, поле.выс);
    для в от 0 до поле.выс-1 цикл начало
        длина(поле.клетки[в], поле.шир);
        {если в > поле.выс-6 то
            для г от 0 до поле.шир-1 цикл
                если случайное(поле.выс-в) = 0 то
                    поле.клетки[в][г] := случайное(5)+1;}
    конец;
окончание;

функция создатьФигуру(ид: целое; г, в: целые): Фигура;
переменная
    ф: Фигура;
начало
    ф.ид := ид;
    ф.г := г;
    ф.в := в;
    длина(ф.клетки, 4);
    выбор ид из
        1: начало // палка
            ф.клетки[0] := тчк(-1, 0);
            ф.клетки[1] := тчк(0, 0);
            ф.клетки[2] := тчк(1, 0);
            ф.клетки[3] := тчк(2, 0);
        конец;
        2: начало // Г
            ф.клетки[0] := тчк(-1, -1);
            ф.клетки[1] := тчк(-1, 0);
            ф.клетки[2] := тчк(0, 0);
            ф.клетки[3] := тчк(1, 0);
        конец;
        3: начало // L
            ф.клетки[0] := тчк(-1, 0);
            ф.клетки[1] := тчк(0, 0);
            ф.клетки[2] := тчк(1, 0);
            ф.клетки[3] := тчк(1, -1);
        конец;
        4: начало // T
            ф.клетки[0] := тчк(-1, 0);
            ф.клетки[1] := тчк(0, 0);
            ф.клетки[2] := тчк(1, 0);
            ф.клетки[3] := тчк(0, -1);
        конец;
        5: начало // кубик
            ф.клетки[0] := тчк(0, -1);
            ф.клетки[1] := тчк(0, 0);
            ф.клетки[2] := тчк(1, -1);
            ф.клетки[3] := тчк(1, 0);
        конец;
        6: начало // S
            ф.клетки[0] := тчк(-1, 0);
            ф.клетки[1] := тчк(0, 0);
            ф.клетки[2] := тчк(0, -1);
            ф.клетки[3] := тчк(1, -1);
        конец;
        7: начало // Z
            ф.клетки[0] := тчк(-1, 0);
            ф.клетки[1] := тчк(0, 0);
            ф.клетки[2] := тчк(0, 1);
            ф.клетки[3] := тчк(1, 1);
        конец;
    конец;
    вернуть ф;
окончание;

функция помещается(ф: Фигура): логическое;
переменная
    и: целое;
    г, в: целые;
начало
    для каждого и из ф.клетки цикл начало
        г := ф.г + ф.клетки[и].г;
        в := ф.в + ф.клетки[и].в;
        если г < 0 || г >= поле.шир то вернуть нет;
        если в < 0 || в >= поле.выс то вернуть нет;
        если поле.клетки[в][г] <> 0 то вернуть нет;
    конец;
    вернуть да;
окончание;

процедура поместить(ф: Фигура; дбв: логическое);
переменная
    и: целое;
    г, в: целые;
начало
    для каждого и из ф.клетки цикл начало
        г := ф.г + ф.клетки[и].г;
        в := ф.в + ф.клетки[и].в;
        если дбв то поле.клетки[в][г] := ф.ид
        иначе поле.клетки[в][г] := 0;
    конец;
окончание;

функция повернуть(ф: Фигура; поЧС: логическое): Фигура;
переменная
    и: целое;
    г, в: целые;
начало
    для каждого и из ф.клетки цикл начало
        г := ф.клетки[и].г;
        в := ф.клетки[и].в;
        если поЧС то начало
            ф.клетки[и].в := г;
            ф.клетки[и].г := -в;
        конец иначе начало
            ф.клетки[и].в := -г;
            ф.клетки[и].г := в;
        конец;
    конец;
    вернуть ф;
окончание;

функция подвинуть(ф: Фигура; дг, дв: целые): Фигура;
начало
    ф.г += дг;
    ф.в += дв;
    вернуть ф;
окончание;

процедура инициализация();
переменная
    и: целое;
    скорость: целое = 2;
начало
    // цвета фигур
    длина(цвета, всегоФигур+1);
    цвета[0] := 0;
    для и от 1 до всегоФигур цикл цвета[и] := и+8;
    // клавиатура
    клавиши[""#1b"[D"] := клВлево;
    клавиши[""#1b"[C"] := клВправо;
    клавиши[""#1b"[H"] := клПрЧС;
    клавиши[""#1b"[A"] := клПрЧС;
    клавиши[""#1b"[E"] := клПрЧС;
    клавиши[""#1b"[5~"] := клПоЧС;
    клавиши[""#1b"[B"] := клСбросить;
    клавиши["Q"] := клВыход;
    клавиши["q"] := клВыход;
    клавиши["Й"] := клВыход;
    клавиши["й"] := клВыход;
    клавиши[""#20] := клПауза;
    // аргументы
    поле.шир := 20;
    поле.выс := 25;
    если длина(арг) >= 1 то скорость := целое(арг[0]);
    если длина(арг) >= 2 то поле.шир := целое(арг[1]);
    если длина(арг) >= 3 то поле.выс := целое(арг[2]);
    если поле.шир < 8 то поле.шир := 8
    иначе если поле.шир > 160 то поле.шир := 160;
    если поле.выс < 10 то поле.выс := 10
    иначе если поле.выс > 50 то поле.выс := 50;
    выбор скорость из
        1: скоростьСпуска := 30;
        2: скоростьСпуска := 20;
        3: скоростьСпуска := 10;
        4: скоростьСпуска := 5;
        иначе скоростьСпуска := 1;
    конец;
    создатьПоле();
окончание;

функция клавиша(): целое;
переменная
    с: строка = "";
начало
    пока естьСимвол() цикл
        с := с ++ прочестьСимвол();
    если нету(клавиши[с]) то вернуть клНет
    иначе вернуть клавиши[с];
окончание;

процедура отрисовать(г0, в0: целые);
переменная
    цв: целое;
    г, в: целые;
    с: строка;
начало
    если заставка то начало
        курсор(0, 2);
        цветШрифта(цвет256(192, 192, 0));
        с := "                    ";
        вывести(с ++ "▒▒▒▒▒▒ ▒▒▒▒▒▒ ▒▒▒▒▒▒ ▒▒▒▒▒  ▒▒  ▒▒  ▒▒▒▒ ", НС);
        вывести(с ++ "  ▒▒   ▒▒       ▒▒   ▒▒  ▒▒ ▒▒ ▒▒▒ ▒▒  ▒▒", НС);
        вывести(с ++ "  ▒▒   ▒▒▒▒     ▒▒   ▒▒  ▒▒ ▒▒▒▒▒▒ ▒▒    ", НС);
        вывести(с ++ "  ▒▒   ▒▒       ▒▒   ▒▒▒▒▒  ▒▒▒ ▒▒ ▒▒  ▒▒", НС);
        вывести(с ++ "  ▒▒   ▒▒▒▒▒▒   ▒▒   ▒▒     ▒▒  ▒▒  ▒▒▒▒ ", НС, НС, НС);
        сброситьАтрибуты();
        вывести("  Параметры командной строки:", НС);
        вывести("    klaus тетрис.клаус <скорость> <ширина-поля> <высота-поля>", НС, НС);
        вывести("  Управление:", НС);
        вывести("    Вправо, влево     -- переместить фигуру,", НС);
        вывести("    Вверх, PgUp, PgDn -- повернуть фигуру,", НС);
        вывести("    Вниз              -- сбросить фигуру,", НС, НС);
        вывести("    Пробел            -- пауза,", НС, НС);
        вывести("    Q -- закончить игру.", НС, НС);
        вывести("  Нажмите ПРОБЕЛ для начала игры или Q для выхода...", НС);
        вернуться;
    конец;
    для в от 0 до поле.выс-1 цикл начало
        курсор(г0, в0+в);
        цв := цветРамки;
        цветШрифта(цв);
        с := смвЛвРамка;
        для г от 0 до поле.шир-1 цикл начало
            если поле.клетки[в][г] = 0 то 
                с := с ++ "  "
            иначе начало
                если цв <> цвета[поле.клетки[в][г]] то начало
                    вывести(с);
                    с := "";
                    цв := цвета[поле.клетки[в][г]];
                    цветШрифта(цв);
                конец;
                с := с ++ стрБлок;
            конец;
        конец;
        если цв <> цветРамки то начало
            вывести(с);
            цветШрифта(цветРамки);
            с := смвПрРамка;
        конец иначе
            с := с ++ смвПрРамка;
        вывести(с);
    конец;
    с := "";
    для г от 0 до поле.шир*2-1 цикл с := с ++ смвНжРамка;
    цветШрифта(цветРамки);
    курсор(г0+1, в0+поле.выс);
    вывести(с);
    с := формат("Счёт: %ц", очки);
    курсор(г0+2+поле.шир-след(с, 0, максЦелое)\2, в0+поле.выс+2);
    вывести(с);
окончание;

процедура проверитьЗаполненные();

    функция полная(в: целое): логическое;
    переменная
        и: целое;
    начало
        для и от 0 до поле.шир-1 цикл
            если поле.клетки[в][и] = 0 то вернуть нет;
        вернуть да;
    окончание;

переменная
    в, в0: целые;
    коэф: дробное;
начало
    коэф := 1;
    в0 := поле.выс-1;
    для в от поле.выс-1 до 0 обратный цикл начало
        если в0 <> в то поле.клетки[в0] := поле.клетки[в];
        если !полная(в) то 
            в0 -= 1
        иначе начало
            очки += округл(поле.шир*коэф);
            коэф *= 1.5;
        конец;
    конец;
окончание;

переменная
    скорость: целое;
    задержка: целое;
    следующая: логическое;
    стоп: логическое = нет;
    ф, нф: Фигуры;
начало
    инициализация();
    терминал(1, да);
    размерЭкрана(80, 25);
    очиститьЭкран();
    скрытьКурсор();
    заставка := да;
    стоп := да;
    пока ДА цикл начало
        если !стоп && следующая то начало
            следующая := нет;
            проверитьЗаполненные();
            ф := создатьФигуру(случайное(всегоФигур)+1, поле.шир\2-1, 2);
            если помещается(ф) то поместить(ф, да)
            иначе завершить(0);
            скорость := скоростьСпуска;
            задержка := скорость;
        конец;
        отрисовать(2, 2);
        выбор клавиша() из
            клВлево: если !стоп то начало
                поместить(ф, нет);
                нф := подвинуть(ф, -1, 0);
                если помещается(нф) то ф := нф;
                поместить(ф, да);
            конец;
            клВправо: если !стоп то начало
                поместить(ф, нет);
                нф := подвинуть(ф, 1, 0);
                если помещается(нф) то ф := нф;
                поместить(ф, да);
            конец;
            клПоЧС: если !стоп то начало
                поместить(ф, нет);
                нф := повернуть(ф, да);
                если помещается(нф) то ф := нф;
                поместить(ф, да);
            конец;
            клПрЧС: если !стоп то начало
                поместить(ф, нет);
                нф := повернуть(ф, нет);
                если помещается(нф) то ф := нф;
                поместить(ф, да);
            конец;
            клСбросить: если !стоп то начало
                скорость := скоростьПадения;
                задержка := скорость;
            конец;
            клПауза: если заставка то начало
                стоп := нет;
                заставка := нет;
                задержка := скорость;
                следующая := да;
                размерЭкрана(поле.шир*2+6, поле.выс+6);
                очиститьЭкран();
                скрытьКурсор();
                продолжить;
            конец иначе 
                стоп := !стоп;
            клВыход: завершить 0;
        конец;
        если !стоп то начало
            если задержка > 0 то
                задержка -= 1
            иначе начало
                поместить(ф, нет);
                нф := подвинуть(ф, 0, 1);
                если помещается(нф) то ф := нф
                иначе следующая := да;
                поместить(ф, да);
                задержка := скорость;
            конец;
        конец;
        пауза(20);
    конец;
напоследок
    //очиститьЭкран();
    показатьКурсор();
    терминал(1, нет);
окончание.
