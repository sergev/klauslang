программа Тетрис(арг: массив строк);

типы
    ИгровоеПоле = структура
        шир: целое;
        выс: целое;
        клетки: массив массивов целых;
    окончание;
    
    Фигура/Фигуры/Фигур = структура
        ид: целое;
        г, в: целые;
        клетки: массив Точек;
    окончание;

константы
    стрБлок: строка = ""№2588№2588;
    смвЛвРамка = №2590;
    смвПрРамка = №258Ц;
    смвНжРамка = №2580;
    
константы
    клНет = 0;
    клВлево = 1;
    клВправо = 2;
    клПоЧС = 3;
    клПрЧС = 4;
    клСбросить = 5;
    клВыход = 6;
    клПауза = 7;

константа
    клавиши: словарь целых ключ строка = {
        ""#1b"[D" : клВлево,
        ""#1b"[C" : клВправо,
        ""#1b"[H" : клПрЧС,
        ""#1b"[A" : клПрЧС,
        ""#1b"[E" : клПрЧС,
        ""#1b"[5~": клПоЧС,
        ""#1b"[B" : клСбросить,
        "Q"       : клВыход,
        "q"       : клВыход,
        "Й"       : клВыход,
        "й"       : клВыход,
        #20       : клПауза};

константы
    всегоФигур = 7;
    цветаФигур: массив целых = [0, 9, 10, 11, 12, 13, 14, 15];
    клеткиФигур: массив массивов Точек = [
        [{г = -1, в =  0}, {г =  0, в = 0}, {г = 1, в =  0}, {г = 2, в =  0}],  // палка
        [{г = -1, в = -1}, {г = -1, в = 0}, {г = 0, в =  0}, {г = 1, в =  0}],  // Г
        [{г = -1, в =  0}, {г =  0, в = 0}, {г = 1, в =  0}, {г = 1, в = -1}],  // L
        [{г = -1, в =  0}, {г =  0, в = 0}, {г = 1, в =  0}, {г = 0, в = -1}],  // Т
        [{г =  0, в = -1}, {г =  0, в = 0}, {г = 1, в = -1}, {г = 1, в =  0}],  // кубик
        [{г = -1, в =  0}, {г =  0, в = 0}, {г = 0, в = -1}, {г = 1, в = -1}],  // S
        [{г = -1, в =  0}, {г =  0, в = 0}, {г = 0, в =  1}, {г = 1, в =  1}]]; // Z

переменные
    цветРамки: целое = 7;
    поле: ИгровоеПоле;
    скоростьСпуска: целое = 20;
    скоростьПадения: целое = 0;
    очки: целое;
    заставка: логическое;

процедура создатьПоле();
переменные
    к: целые;
начало
    длина(поле.клетки, поле.выс);
    для к от 0 до поле.выс-1 цикл 
        длина(поле.клетки[к], поле.шир); 
окончание;

функция создатьФигуру(ид: целое; г, в: целые): Фигура;
переменная
    ф: Фигура;
начало
    ф.ид := ид;
    ф.г := г;
    ф.в := в;
    ф.клетки := клеткиФигур[ид-1];
    вернуть ф;
окончание;

функция помещается(ф: Фигура): логическое;
переменные
    к: целое;
    г, в: целые;
начало
    для каждого к из ф.клетки цикл начало
        г := ф.г + ф.клетки[к].г;
        в := ф.в + ф.клетки[к].в;
        если г < 0 или г >= поле.шир то вернуть нет;
        если в < 0 или в >= поле.выс то вернуть нет;
        если поле.клетки[в][г] <> 0 то вернуть нет;
    конец;
    вернуть да;
окончание;

процедура поместить(ф: Фигура; стереть: логическое);
переменные
    к: целое;
    г, в: целые;
начало
    для каждого к из ф.клетки цикл начало
        г := ф.г + ф.клетки[к].г;
        в := ф.в + ф.клетки[к].в;
        если стереть то поле.клетки[в][г] := 0
        иначе поле.клетки[в][г] := ф.ид;
    конец;
окончание;

функция повернуть(ф: Фигура; поЧС: логическое): Фигура;
переменные
    к: целое;
    г, в: целые;
начало
    для каждого к из ф.клетки цикл начало
        г := ф.клетки[к].г;
        в := ф.клетки[к].в;
        если поЧС то начало
            ф.клетки[к].в := г;
            ф.клетки[к].г := -в;
        конец иначе начало
            ф.клетки[к].в := -г;
            ф.клетки[к].г := в;
        конец;
    конец;
    вернуть ф;
окончание;

функция подвинуть(ф: Фигура; дг, дв: целые): Фигура;
начало
    ф.г += дг;
    ф.в += дв;
    вернуть ф;
окончание;

процедура инициализация();
переменные
    к: целое;
    скорость: целое = 2;
начало
    поле.шир := 20;
    поле.выс := 25;
    если длина(арг) >= 1 то скорость := целое(арг[0]);
    если длина(арг) >= 2 то поле.шир := целое(арг[1]);
    если длина(арг) >= 3 то поле.выс := целое(арг[2]);
    если поле.шир < 8 то поле.шир := 8
    иначе если поле.шир > 160 то поле.шир := 160;
    если поле.выс < 10 то поле.выс := 10
    иначе если поле.выс > 50 то поле.выс := 50;
    выбор скорость из
        1: скоростьСпуска := 30;
        2: скоростьСпуска := 20;
        3: скоростьСпуска := 10;
        4: скоростьСпуска := 5;
        иначе скоростьСпуска := 1;
    конец;
    создатьПоле();
окончание;

функция клавиша(): целое;
переменная
    с: строка = "";
начало
    пока естьСимвол() цикл
        с := с ++ прочестьСимвол();
    если нету(клавиши[с]) то вернуть клНет
    иначе вернуть клавиши[с];
окончание;

процедура отрисовать(г0, в0: целые);
переменные
    цв: целое;
    г, в: целые;
    с: строка;
начало
    если заставка то начало
        курсор(0, 2);
        цветШрифта(цвет256(192, 192, 0));
        с := "                    ";
        вывести(с ++ "▒▒▒▒▒▒ ▒▒▒▒▒▒ ▒▒▒▒▒▒ ▒▒▒▒▒  ▒▒  ▒▒  ▒▒▒▒ ", НС);
        вывести(с ++ "  ▒▒   ▒▒       ▒▒   ▒▒  ▒▒ ▒▒ ▒▒▒ ▒▒  ▒▒", НС);
        вывести(с ++ "  ▒▒   ▒▒▒▒     ▒▒   ▒▒  ▒▒ ▒▒▒▒▒▒ ▒▒    ", НС);
        вывести(с ++ "  ▒▒   ▒▒       ▒▒   ▒▒▒▒▒  ▒▒▒ ▒▒ ▒▒  ▒▒", НС);
        вывести(с ++ "  ▒▒   ▒▒▒▒▒▒   ▒▒   ▒▒     ▒▒  ▒▒  ▒▒▒▒ ", НС, НС, НС);
        сброситьАтрибуты();
        вывести("  Параметры командной строки:", НС);
        вывести("    klaus тетрис.клаус <скорость> <ширина-поля> <высота-поля>", НС, НС);
        вывести("  Управление:", НС);
        вывести("    Вправо, влево     -- переместить фигуру,", НС);
        вывести("    Вверх, PgUp, PgDn -- повернуть фигуру,", НС);
        вывести("    Вниз              -- сбросить фигуру,", НС, НС);
        вывести("    Пробел            -- пауза,", НС, НС);
        вывести("    Q -- закончить игру.", НС, НС);
        вывести("  Нажмите ПРОБЕЛ для начала игры или Q для выхода...", НС);
        вернуться;
    конец;
    для в от 0 до поле.выс-1 цикл начало
        курсор(г0, в0+в);
        цв := цветРамки;
        цветШрифта(цв);
        с := смвЛвРамка;
        для г от 0 до поле.шир-1 цикл начало
            если поле.клетки[в][г] = 0 то 
                с := с ++ "  "
            иначе начало
                если цв <> цветаФигур[поле.клетки[в][г]] то начало
                    вывести(с);
                    с := "";
                    цв := цветаФигур[поле.клетки[в][г]];
                    цветШрифта(цв);
                конец;
                с := с ++ стрБлок;
            конец;
        конец;
        если цв <> цветРамки то начало
            вывести(с);
            цветШрифта(цветРамки);
            с := смвПрРамка;
        конец иначе
            с := с ++ смвПрРамка;
        вывести(с);
    конец;
    с := "";
    для г от 0 до поле.шир*2-1 цикл с := с ++ смвНжРамка;
    цветШрифта(цветРамки);
    курсор(г0+1, в0+поле.выс);
    вывести(с);
    с := формат("Счёт: %ц", очки);
    курсор(г0+2+поле.шир-след(с, 0, максЦелое)\2, в0+поле.выс+2);
    вывести(с);
окончание;

процедура проверитьЗаполненные();

    функция полная(в: целое): логическое;
    переменная
        к: целое;
    начало
        для к от 0 до поле.шир-1 цикл
            если поле.клетки[в][к] = 0 то вернуть нет;
        вернуть да;
    окончание;

переменные
    в, в0: целые;
    коэф: дробное;
начало
    коэф := 1;
    в0 := поле.выс-1;
    для в от поле.выс-1 до 0 обратный цикл начало
        если в0 <> в то поле.клетки[в0] := поле.клетки[в];
        если не полная(в) то 
            в0 -= 1
        иначе начало
            очки += округл(поле.шир*коэф);
            коэф *= 1.5;
        конец;
    конец;
окончание;

переменные
    скорость: целое;
    задержка: целое;
    следующая: логическое;
    стоп: логическое = нет;
    ф, нф: Фигуры;
начало
    инициализация();
    терминал(идСтдВывод, трСквозной);
    размерЭкрана(80, 25);
    очиститьЭкран();
    скрытьКурсор();
    заставка := да;
    стоп := да;
    пока ДА цикл начало
        если не стоп и следующая то начало
            следующая := нет;
            проверитьЗаполненные();
            ф := создатьФигуру(случайное(всегоФигур)+1, поле.шир\2-1, 2);
            если помещается(ф) то поместить(ф, нет)
            иначе завершить 0;
            скорость := скоростьСпуска;
            задержка := скорость;
        конец;
        отрисовать(2, 2);
        выбор клавиша() из
            клВлево: если не стоп то начало
                поместить(ф, да);
                нф := подвинуть(ф, -1, 0);
                если помещается(нф) то ф := нф;
                поместить(ф, нет);
            конец;
            клВправо: если не стоп то начало
                поместить(ф, да);
                нф := подвинуть(ф, 1, 0);
                если помещается(нф) то ф := нф;
                поместить(ф, нет);
            конец;
            клПоЧС: если не стоп то начало
                поместить(ф, да);
                нф := повернуть(ф, да);
                если помещается(нф) то ф := нф;
                поместить(ф, нет);
            конец;
            клПрЧС: если не стоп то начало
                поместить(ф, да);
                нф := повернуть(ф, нет);
                если помещается(нф) то ф := нф;
                поместить(ф, нет);
            конец;
            клСбросить: если не стоп то начало
                скорость := скоростьПадения;
                задержка := скорость;
            конец;
            клПауза: если заставка то начало
                стоп := нет;
                заставка := нет;
                задержка := скорость;
                следующая := да;
                размерЭкрана(поле.шир*2+6, поле.выс+6);
                очиститьЭкран();
                скрытьКурсор();
                продолжить;
            конец иначе 
                стоп := не стоп;
            клВыход: завершить 0;
        конец;
        если не стоп то начало
            если задержка > 0 то
                задержка -= 1
            иначе начало
                поместить(ф, да);
                нф := подвинуть(ф, 0, 1);
                если помещается(нф) то ф := нф
                иначе следующая := да;
                поместить(ф, нет);
                задержка := скорость;
            конец;
        конец;
        пауза(20);
    конец;
напоследок
    показатьКурсор();
    терминал(идСтдВывод, трКанон);
окончание.
